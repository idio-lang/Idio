;;
;; Copyright (c) 2015 Ian Fitchet <idf(at)idio-lang.org>
;;
;; Licensed under the Apache License, Version 2.0 (the "License"); you
;; may not use this file except in compliance with the License.  You
;; may obtain a copy of the License at
;;
;;     http://www.apache.org/licenses/LICENSE-2.0
;;
;; Unless required by applicable law or agreed to in writing, software
;; distributed under the License is distributed on an "AS IS" BASIS,
;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;; See the License for the specific language governing permissions and
;; limitations under the License.
;;
;;

;;
;; test.idio
;;

;; largely ported from
;;
;; Scheme 9 from Empty Space
;; By Nils M Holm <nmh@t3x.org>, 2007,2008,2009


testfile := "__testfile__"

if (file-exists? testfile) {
  delete-file testfile
}
    ;; (error (string-append "Please delete the file \""
    ;;                       testfile
    ;;                       "\" before running this test.")))

Errors := 0
Tests := 0

define (void) (if #f #f)

define (seq) {
  n := 1

  function () {
    x := n
    set! n (1 + n)
    x
  }
}

define (fail expr result expected) {
  set! Errors (1 + Errors)
  display* "Error #" Errors
  display* "test #" Tests " failed: " expr
  display* "got result:  " result 
  display* "expected:    " expected 
}

define (test3 expr result expected) {
  set! Tests (1 + Tests)
  ;display* "test #" Tests ":" expr "=>" result
  if (not (equal? result expected)) {
    fail expr result expected
  }
}

define-macro (test form result) {
  %T{ test3 '$form $form $result }
}

display* "This is test.idio"

;; tinker with the PATH
OPATH := PATH
PATH = string-append PATH ":" (string-append PWD "/../utils") 

;; at least one test creates a local file then tries to "load" it --
;; so we need PWD on IDIOLIB
OIDIOLIB := IDIOLIB
IDIOLIB = string-append IDIOLIB ":" PWD

load "tests/s9-test.idio"
load "tests/operator.idio"
load "tests/template.idio"
load "tests/array.idio"
load "tests/file-handle.idio"
load "tests/string-handle.idio"
load "tests/handle.idio"
load "tests/struct.idio"
load "tests/condition.idio"
load "tests/dynamic.idio"
load "tests/call-cc.idio"
load "tests/env.idio"
load "tests/command.idio"
load "tests/libc-wrap.idio"

;; test that we've seen as many tests as we should have.  How many is
;; that?  Erm, they've not been counted and are dynamic in number so
;; test we've seen at least as many as the largest number we've
;; seen.  Wait, some tests are OS-specific.  Drat!
;test (eq? Tests 2820) #t
;test (ge Tests 2820) #t

if+ (zero? Errors) (display* "All" Tests "tests passed!") \
    else {
      display Errors
      if (gt Errors 1) {
        display " errors "
      } {
	display " error "
      }
      display* "in" Tests "tests."
    }

if (file-exists? testfile) (delete-file testfile)

IDIOLIB = OIDIOLIB
PATH = OPATH
